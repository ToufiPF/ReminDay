plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'jacoco'
}

android {
    namespace 'ch.epfl.reminday'
    compileSdk 33
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId "ch.epfl.reminday"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 4
        versionName "1.2.0"

        multiDexEnabled = true
        testInstrumentationRunner "ch.epfl.reminday.HiltTestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
        }
    }

    kotlinOptions {
        jvmTarget = java_version.toString()
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version

        coreLibraryDesugaringEnabled = true
    }
    testOptions {
        animationsDisabled = true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    buildFeatures {
        viewBinding true
        dataBinding false
    }
    // set version of underlying AGP jacoco plugin
    jacoco {
        version jacoco_version
    }
}
// set version of the project's jacoco plugin
jacoco {
    version jacoco_version
    toolVersion jacoco_version
}
// workaround from https://issuetracker.google.com/issues/171125857
// don't instrument the code, it's already done by AGP
tasks.withType(Test) {
    jacoco {
        includeNoLocationClasses = true
        excludes = ["*"]
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha05'
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha04'
    implementation 'net.zetetic:android-database-sqlcipher:4.5.3'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    // TODO: Update to 2.0.0 when AGP 7.4.0 is released/supported
    //noinspection GradleDependency
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.6'

    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def paging_version = "3.1.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Hilt (dependency injection)
    implementation "com.google.dagger:hilt-android:$hilt_version"
    debugImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Preferences (Compat)
    implementation "androidx.preference:preference-ktx:1.2.0"

    // Faker (fake data generation)
    implementation 'io.github.serpro69:kotlin-faker:1.13.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"

    // Mockk is way better than mockito for kotlin (but only available for unit tests)
    testImplementation 'io.mockk:mockk:1.13.3'
    androidTestImplementation 'org.mockito:mockito-android:4.10.0' // Mockito android

    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation "androidx.work:work-testing:$work_version" // for testing Workers

    // Espresso 3.4.0 is buggy (java.lang.NoSuchMethodError: No static method loadSingleServiceOrNull) -> onView()
    // Espresso 3.5.0 is buggy (test hangs)
    def espresso_version = "3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    implementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
    // UiAutomator: manage device (eg. navigate outside of app, display notifications...)
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

// create new task for code coverage with Jacoco
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    // Exclude generated classes
    def excluded = [
            // Android
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*Binding.class',
            // Hilt
            '**/*Hilt*.*',
            '**/hilt_aggregated_deps/**',
            '**/*_Factory.class',
            '**/*_MembersInjector.class',
            '**/dagger/**',
            // Room
            '**/*_Impl*.*',
    ]

    def mainSrc = "$project.projectDir/src/main/java"
    sourceDirectories.from = files([mainSrc])

    def debugKotlinClasses = fileTree(
            dir: "$project.buildDir/tmp/kotlin-classes/debug",
            excludes: excluded,
    )
    classDirectories.from = files([debugKotlinClasses])

    executionData.from = fileTree(dir: project.buildDir, includes: [
            // unit test coverage:
            'jacoco/testDebugUnitTest.exec', // generated by plugin
            'outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec', // generated by AGP
            // android coverage:
            'outputs/code_coverage/debugAndroidTest/connected/*coverage.ec'
    ])

    doLast {
        println "Coverage report at: file:///$project.buildDir/reports/jacoco/jacocoTestReport/html/index.html\n"
    }
}

connectedCheck {
    finalizedBy jacocoTestReport
}
